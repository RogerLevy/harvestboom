include config/options.vfx
include config/allegro.vfx
cwd ../vfxland3
include lib/vl3/vfxland3.vfx
include lib/csv.vfx
include plugins/obj1.vfx
include plugins/bitmaps2.vfx
cwd %idir%

%object sizeof
objvar tm-cols  \ scalar
objvar tm-rows  \ scalar
objvar tm-dcols  \ scalar
objvar tm-drows  \ scalar
objvar tm-base  \ addr
objvar tm-stride
objvar tm-scrollx  \ scalar
objvar tm-scrolly  \ scalar
objvar tm-tw  \ scalar
objvar tm-th  \ scalar
objvar tm-ts
create %tilemap ,

include ../vfxland3/plugins/tilemap2.vfx
include ../vfxland3/plugins/tilecol1.vfx
include ../vfxland3/lib/files1.vfx

-sin  \ disable tokenization of all definitions from here

\ ------------------------------------------------------------------------------

create alevt /ALLEGRO_EVENT allot&erase

\ ------------------------------------------------------------------------------

: read> ( $ c - <code> )  ( a c - )
    r/o[ bytes-left allocate throw
        dup bytes-left 2dup read  r> execute
        free throw
    ]file ;

create crlf 2 c, $0d c, $0a c, 
: lines> ( a c - <code> ) ( a c - )
    begin over >r crlf count search while
        over r> swap over - r@ execute
        2 /string
    repeat
    r> drop
    dup if  r@ execute  else  2drop  then
    r> drop ;

\ ------------------------------------------------------------------------------

: static-map  create read> lines> csv> evaluate , ;
s" data/leveltest.csv" static-map testmap
z" data/castle-tileset.png" loadbmp constant castle.bmp
create castle.ts  castle.bmp 16 16 >tiles,

: create-static-tilemap ( a-map a-ts cols rows - <name> ) ( - tilemap )
    create %tilemap static /tilemap ;

\ ------------------------------------------------------------------------------

: move-about
    0 0 vx v!
    <up> held? if -1 vy s! then
    <down> held? if 1 vy s! then
    <left> held? if -1 vx s! then
    <right> held? if 1 vx s! then ;
\    <enter> pressed if 10 x s+! then
\    walt y +! x s+! ;

: extend  sizeof ;
%object extend
objvar map
create %barrel ,

: /barrel
    16 16 w v!
    step> move-about
    motion>  map @ ?dup if  collide-tilemap  then  vx fv@ x fv+!  
    draw> 58 castle.ts tile ;

\ ------------------------------------------------------------------------------

create root 0 0 pool
testmap castle.ts 20 15 create-static-tilemap tm 
create barrel %barrel static /barrel  tm map !  50 50 x v!  0 0 vx v!

\ ------------------------------------------------------------------------------

: handle-events
    begin  queue alevt al_get_next_event  while
        \ alevt ALLEGRO_EVENT.type @ ALLEGRO_EVENT_MOUSE_BUTTON_DOWN = if
        \ then 
    repeat ;

: >display
    reset-keyboard 
    display al_get_win_window_handle SetForegroundWindow drop
    identity 0e 0e 0e 2e 2e transformed ;

: >vfx
    mswin? if
        vfx-hwnd SetForegroundWindow
    then ;

: poll  poll-keyboard poll-mouse ;
: cls  0.5e 0.25e 0e 1e al_clear_to_color ;
: think  each>  'step @ ?execute  -1 ;
: motion  each>  'motion @ ?execute  -1 ;
: advance  poll  handle-events  dup think  motion ;
: render  each>  x v@ at  'draw @ ?execute  -1 ; 
: present  display al_flip_display ;
: frame  root advance  cls  root render  present ;
: go  >display  begin frame pause  <escape> pressed? until  >vfx ;

\ go